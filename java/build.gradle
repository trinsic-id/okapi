buildscript {
    repositories {
        mavenLocal()
        mavenCentral()

        maven { url "https://repo1.maven.org/maven2/" }
    }
}
plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
}

// Gradle doesn't allow these at the top of the file - go figure?
def jarDomainId = "id"
def jarGroupId = "trinsic"
def jarArtifactId = "okapi"
def jarVersion = "1.6.0"

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/trinsic-id/okapi"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = jarDomainId + "." + jarGroupId
            artifactId = jarArtifactId
            version = jarVersion
            from(components.java)
        }
    }
}

group = jarDomainId + "." + jarGroupId + '.' + jarArtifactId
archivesBaseName = 'trinsic-' + jarArtifactId
version = jarVersion

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:22.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.21.5'
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.8.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
}

jar {
    archiveBaseName.set(getArchivesBaseName())
    archiveVersion.set(jarVersion)
}
